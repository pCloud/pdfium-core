buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.9.1'
    }
}

plugins {
    id 'maven-publish'
}

apply plugin: 'com.android.library'

android {
    namespace = "com.shockwave.pdfium"
    ndkVersion = "29.0.13113456"
    compileSdkVersion 35

    buildFeatures.buildConfig = true

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 35
        versionCode 1
        versionName "2.0.0"

        buildConfigField("String", 'VERSION_NAME', "\"${versionName}\"")

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.toVersion(JavaVersion.VERSION_11)
        targetCompatibility JavaVersion.toVersion(JavaVersion.VERSION_11)
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    sourceSets{
        main {
            jni.srcDirs = []
            jniLibs.srcDir 'src/main/libs'
        }
    }

    packagingOptions{
        doNotStrip "*.so"
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.core:core:1.15.0'
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.pcloud'
                artifactId = 'pdfium-core'
                version = '2.0.0'
            }
        }
    }
}